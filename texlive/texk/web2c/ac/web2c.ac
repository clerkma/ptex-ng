dnl $Id: web2c.ac 72216 2024-09-07 16:42:07Z karl $
# texk/web2c/ac/web2c.ac: configure.ac fragment for TL subdir
dnl
dnl Copyright 2015-2024 Karl Berry <tex-live@tug.org>
dnl Copyright 2009-2015 Peter Breitenlohner <tex-live@tug.org>
dnl You may freely use, modify and/or distribute this file.
dnl
# configure options for TeX and MF
m4_define_default([kpse_indent_26], [26])[]dnl
AC_ARG_WITH([banner-add],
            AS_HELP_STRING([--with-banner-add=STR],
                           [add STR to version string appended to banner lines]))[]dnl
AC_ARG_WITH([editor],
            AS_HELP_STRING([--with-editor=CMD],
                           [invoke CMD from the `e' option @<:@vi +%d '%s'@:>@
                            or @<:@texworks --position=%d "%s"@:>@]))[]dnl
AC_ARG_ENABLE([auto-core],
              AS_HELP_STRING([--enable-auto-core],
                             [cause TeX&MF to dump core, given a certain filename],
                             kpse_indent_26))[]dnl
AC_ARG_ENABLE([dump-share],
              AS_HELP_STRING([--disable-dump-share],
                             [make fmt/base files architecture-dependent],
                             kpse_indent_26))[]dnl
## configure options for TeX
AC_ARG_ENABLE([ipc],
              AS_HELP_STRING([--disable-ipc],
                             [disable TeX's --ipc option, i.e., pipe to a program],
                             kpse_indent_26))[]dnl
dnl [PROG, BUILD-OR-NO, SYNC-OR-NO, TEXT, REQUIRED-LIBS]
m4_define([kpse_tex_progs], [dnl
[[tex],       [yes], [no],  [TeX],        []],
[[etex],      [no],  [yes], [e-TeX],      [zlib]],
[[ptex],      [no],  [yes], [pTeX],       [ptexenc zlib]],
[[uptex],     [no],  [yes], [upTeX],      [ptexenc zlib]],
[[euptex],    [yes], [yes], [e-upTeX],    [ptexenc zlib]],
[[aleph],     [yes], [],    [Aleph],      []],
[[hitex],     [yes], [],    [HiTeX],      [zlib]],
[[pdftex],    [yes], [yes], [pdfTeX],     [xpdf libpng zlib]],
[[luatex],    [yes], [],    [LuaTeX],     [pplib libpng zziplib lua53]],
[[luajittex], [yes], [],    [LuaJITTeX],  [pplib libpng zziplib luajit]],
[[luahbtex],    [yes], [],    [LuaHBTeX],     [pplib libpng zziplib lua53 harfbuzz]],
[[luajithbtex], [yes], [],    [LuaHBJITTeX],  [pplib libpng zziplib luajit harfbuzz]],
[[mp],        [yes], [],    [MetaPost],   [mpfi mpfr cairo libpng]],
[[pmp],       [yes], [],    [pMetaPost],  [mpfi mpfr cairo libpng ptexenc]],
[[texprof],   [yes], [],    [TeXprof],    []],
[[upmp],      [yes], [],    [upMetaPost], [mpfi mpfr cairo libpng ptexenc]],
[[xetex],     [yes], [yes], [XeTeX],      [pplib libpng freetype2 teckit harfbuzz]],
])[]dnl
m4_foreach([Kpse_Prog], [kpse_tex_progs],
           [m4_ifset([Kpse_Prog],
                     [KPSE_WITH_XTEX(Kpse_Prog)])])[]dnl
## configure options for MF
dnl [PROG, BUILD-OR-NO, NOWIN-OR-NO, TEXT, REQUIRED-LIBS]
m4_define([kpse_mf_progs], [dnl
[[mf],       [yes], [yes], [METAFONT], []],
[[mflua],    [yes], [no],  [MFLua],    [lua53 potrace]],
[[mfluajit], [yes], [no],  [MFLuaJIT], [luajit potrace]],
])[]dnl
m4_foreach([Kpse_Prog], [kpse_mf_progs],
           [m4_ifset([Kpse_Prog],
                     [KPSE_WITH_XMF(Kpse_Prog)])])[]dnl
AC_ARG_WITH([mf-x-toolkit],
            AS_HELP_STRING([--with-mf-x-toolkit],
                           [use X toolkit for METAFONT]))[]dnl
m4_define([kpse_mf_win], [dnl
[[epsf],      [EPSF],      [EPSF pseudo]],
[[hp2627],    [HP2627],    [HP 2627]],
[[mftalk],    [MFTALK],    [mftalk (generic server)]],
[[next],      [NEXT],      [NeXT]],
[[regis],     [REGIS],     [Regis]],
[[suntools],  [SUN],       [old Suntools (not X)]],
[[tektronix], [TEKTRONIX], [Tektronix]],
[[uniterm],   [UNITERM],   [Uniterm]],
])[]dnl
m4_foreach([Kpse_Win], [kpse_mf_win],
           [m4_ifset([Kpse_Win],
                     [KPSE_WITH_MFWIN(Kpse_Win)])])[]dnl
## configure options for WEB programs
AC_ARG_ENABLE([web-progs],
              AS_HELP_STRING([--disable-web-progs],
                             [do not build WEB programs bibtex ... weave],
                             kpse_indent_26))[]dnl
## configure option for the SyncTeX library and tool
AC_ARG_ENABLE([synctex],
              AS_HELP_STRING([--disable-synctex],
                             [do not build the SyncTeX library and tool],
                             kpse_indent_26))[]dnl

